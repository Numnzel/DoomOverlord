class spookyspell : spookyspellbase {

	states {
		
		attackfireball:
			AINZ A 1 A_JumpIfInventory("magicatriplet", 1, 2);
			AINZ A 1 A_FireProjectile("magicfireball", 0, FALSE, 6.0, 16.0);
			goto ready;
			AINZ A 0 A_FireProjectile("magicfireball", 8, FALSE, 6.0, 16.0);
			AINZ A 0 A_FireProjectile("magicfireball", -8, FALSE, 6.0, 16.0);
			AINZ A 1 A_FireProjectile("magicfireball", 0, FALSE, 6.0, 16.0);
			goto ready;
		attacklightning:
			AINZ A 0 A_Light(9);
			AINZ A 0 A_StartSound("Spell/Lightning1");
			AINZ A 1 A_RailAttack(350, 0, FALSE, "", "LightBlue", RGF_SILENT|RGF_FULLBRIGHT|RGF_NORANDOMPUFFZ, 600, "", 1, 1, 0, 70, 3.5, 0.05, "magicparticlethunder", 16);
			AINZ A 0 A_Light(0);
			goto ready;
		attackseethrough:
			AINZ A 1 A_GiveInventory("magicsight", 1);
			goto ready;
		attackfly:
			AINZ A 0;
			AINZ A 1 A_GiveInventory("magicfly", 1);
			goto ready;
		attackgravitymaelstrom:
			AINZ A 1 A_FireProjectile("magicgravitymaelstrom", 0, FALSE, 6.0, 16.0);
			goto ready;
		attackantilifecocoon:
			AINZ A 0;
			//AINZ A 1 A_FireBullets(0, 0, 1, 0, "magicantilifecocoon", FBF_NOFLASH|FBF_NORANDOM|FBF_NORANDOMPUFFZ);
			AINZ A 1 A_RailAttack(0, 0, FALSE, "", "", RGF_SILENT|RGF_NORANDOMPUFFZ, 0, "magicantilifepuff");
			goto ready;
		attackastralsmite:
			AINZ A 0;
			AINZ AA 0 A_GiveInventory("castingSpeedSlowWeak", 1);
			AINZ AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 {
				
				A_FireProjectile("magicastralsmite", 0, FALSE, 6.0+frandom(-10.0,10.0), 16.0+frandom(-10.0,10.0));
				A_FireProjectile("magicastralsmite", 0, FALSE, 6.0+frandom(-10.0,10.0), 16.0+frandom(-10.0,10.0));
			}
			goto ready;
		attacknegativeburst:
			AINZ A 0;
			AINZ A 0 A_GiveInventory("castingSpeedSlowWeak", 1);
			AINZ A 0 A_SpawnItemEx("magicnegativeburst", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER);
			AINZ A 0 A_SpawnItemEx("magicnegativeburstexplosion", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER);
			goto ready;
		attackblackhole:
			AINZ A 1;
			AINZ A 8 A_FireProjectile("magicblackholelaunch", 0, FALSE, 6.0, 16.0);
			goto ready;
		attacksummonundead10th:
			AINZ A 1;
			AINZ A 8 {
				
				FLineTraceData RemoteRay;
				LineTrace(
				   angle,
				   256,
				   pitch,
				   flags: TRF_ALLACTORS,
				   offsetz: height-12,
				   data: RemoteRay
				);
				
				if (RemoteRay.HitType == TRACE_HitActor) {
					
					if (RemoteRay.HitActor.bCorpse) {
						
						RemoteRay.HitActor.SetState(Null);
						Actor deathknight = Spawn("revenant", RemoteRay.HitLocation);
						
						if (deathknight) {
							
							deathknight.bFriendly = TRUE;
							deathknight.health = 1000;
							A_Log("A deathknight rises to your side.");
						}
					}
				}
			}
			goto ready;
		attackexplodemine:
			AINZ A 1 A_JumpIfInventory("magicatriplet", 1, 2);
			AINZ A 1 A_FireProjectile("magicexplodemineproj", 0, FALSE, 6.0, 16.0, FPF_NOAUTOAIM, 0);
			goto ready;
			AINZ A 0 A_FireProjectile("magicexplodemineproj", 32, FALSE, 6.0, 16.0, FPF_NOAUTOAIM, 30);
			AINZ A 0 A_FireProjectile("magicexplodemineproj", -32, FALSE, 6.0, 16.0, FPF_NOAUTOAIM, 30);
			AINZ A 1 A_FireProjectile("magicexplodemineproj", 0, FALSE, 6.0, 16.0, FPF_NOAUTOAIM, 0);
			goto ready;
		attackgraspheart:
			AINZ A 1;
			AINZ A 8 {
				
				FLineTraceData RemoteRay;
				LineTrace(
				   angle,
				   256,
				   pitch,
				   flags: TRF_ALLACTORS,
				   offsetz: height-12,
				   data: RemoteRay
				);
				
				if (RemoteRay.HitType == TRACE_HitActor) {
					
					if (!RemoteRay.HitActor.bCorpse && RemoteRay.HitActor.SpawnHealth() < 5000) {
						
						RemoteRay.HitActor.SetStateLabel("Death");
					}
				}
			}
			goto ready;
		attackbodyofrefulgentberyl:
			AINZ A 1;
			AINZ A 0 A_GiveInventory("magicbodyofrefulgentberyl", 1);
			goto ready;
		attacktimestop:
			AINZ A 0;
			AINZ A 1 { A_StartSound("Spell/TimeStopOn", -1, CHANF_OVERLAP|ATTN_NONE|CHANF_NOPAUSE); }
			AINZ A 1;
			AINZ A 1 A_GiveInventory("magictimestop", 1);
			goto ready;
	}
}